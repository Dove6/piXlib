<program> ::= <LEFT_BRACE> <expression_list>* <RIGHT_BRACE>
<expression_list>   ::= <expression> <expression_after_semicolon>*
<expression_after_semicolon> ::= <SEMICOLON> <expression>?
<expression> ::= <global_call> | <value_access> <method_call>?
<global_call>           ::= <AT> <Identifier> <call_head>
<value_access>          ::= <OP_ASTERISK>? <bracket_expression> | <OP_ASTERISK>? <Identifier> | <Literal>
<bracket_expression>    ::= <LEFT_BRACKET> <operation> <RIGHT_BRACKET>
<operation>             ::= <operand> <operator_with_operand>*
<operator_with_operand> ::= <operator> <operand>
<operand>               ::= <bracket_expression> | <IdentifierInOperation> <method_call_in_operation>? | <Literal>
<operator>              ::= <OP_PLUS> | <OP_MINUS> | <OP_ASTERISK> | <OP_AT> | <OP_PERCENT>
<method_call>           ::= <OP_CARET> <Identifier> <call_head>
<call_head>             ::= <LEFT_PARENTHESIS> <argument_list>? <RIGHT_PARENTHESIS>
<argument_list>         ::= <argument> <comma_argument>*
<comma_argument>        ::= <COMMA> <argument>
<argument>              ::= <expression>
<method_call_in_operation> ::= <OP_CARET> <IdentifierInOperation> <call_head_in_operation>
<call_head_in_operation> ::= <LEFT_PARENTHESIS> <argument_list_in_operation>? <RIGHT_PARENTHESIS>
<argument_list_in_operation> ::= <argument_in_operation> <comma_argument_in_operation>*
<comma_argument_in_operation> ::= <COMMA> <argument_in_operation>
<argument_in_operation> ::= <value_access_in_operation> <method_call_in_operation>?
<value_access_in_operation> ::= <bracket_expression> | <IdentifierInOperation> | <Literal>

<Literal>    ::= <String> | <Integer> | <FloatingPoint> | <Boolean>
<String>     ::= "\"" <ALPHANUMERIC> <ALPHANUMERIC>* "\"" | <ALPHANUMERIC> <ALPHANUMERIC>*
<Integer>    ::= "0" | "-"? <NON_ZERO_DIGIT> <DIGIT>*
<NonNegativeInteger> ::= "0" | <NON_ZERO_DIGIT> <DIGIT>*
<FloatingPoint> ::= <Integer> "." <DIGIT> <DIGIT>*
<NonNegativeFloatingPoint> ::= <NonNegativeInteger> "." <DIGIT> <DIGIT>*
<Boolean>    ::= <KW_TRUE> | <KW_FALSE>
<Identifier> ::= <IDENTIFIER_CHAR> <IDENTIFIER_CHAR>*
<IdentifierInOperation> ::= <EXP_IDENTIFIER_CHAR> <EXP_IDENTIFIER_CHAR>*

<IDENTIFIER_CHAR>     ::= "-" | "_" | <ALPHANUMERIC>
<EXP_IDENTIFIER_CHAR> ::= "_" | <ALPHANUMERIC>
<ALPHANUMERIC>    ::= <LETTER> | <DIGIT>
<LETTER>          ::= [A-Z] | [a-z]
<DIGIT>           ::= [0-9]
<NON_ZERO_DIGIT>  ::= [1-9]

<KW_THIS>         ::= "THIS"
<KW_TRUE>         ::= "TRUE"
<KW_FALSE>        ::= "FALSE"

<OP_PLUS>         ::= "+"
<OP_MINUS>        ::= "-"
<OP_ASTERISK>     ::= "*"
<OP_AT>           ::= "@"
<OP_PERCENT>      ::= "%"
<OP_CARET>        ::= "^"

<COMMA>           ::= ","
<HASH>            ::= "#"
<BANG>            ::= "!"
<SEMICOLON>       ::= ";"
<AT>              ::= "@"
<LEFT_PARENTHESIS> ::= "("
<RIGHT_PARENTHESIS> ::= ")"
<LEFT_BRACKET>    ::= "["
<RIGHT_BRACKET>   ::= "]"
<LEFT_BRACE>      ::= "{"
<RIGHT_BRACE>     ::= "}"
